/**
 *
 * Convert value list to a JSON array
 *
 * @param {text} valueList - return delimited value list
 * @param {text} format - "object" or "array", default is object
 * @param {text} [preKey] - OPTIONAL: key string under which to store the values, e.g. "names.first" (or "." for root)
 * @param {text} [postKey - OPTIONAL: if you want an additional key string after the array cell, e.g. "[3].names.last"; note: only for an array
 *
 * @returnSuccess {number} length of the array
 * 
 * @module ValueList
 * 
 * @version 1.1.0
 * @author Allen Ulbricht, https://github.com/aulbricht
 * @website http://northcoastsoftware.com
 *
 */

Let ([

	~list = valueList ;
	~format = format ;
	~format = If ( IsEmpty ( ~format ) ; "object" ; ~format );
	~preKey = preKey ;
	~postKey = postKey ;

	~value = RightValues ( ~list; 1 );
	~value = If ( Right ( ~value ; 1 ) = "¶" ; Left ( ~value ; Length ( ~value ) - 1 ); ~value );
	~count = ValueCount ( ~list );
	~listUpdated = LeftValues ( ~list ; ~count - 1 );
	~jsonInitString =  If ( ~format = "array" ; "[]" ; "{}" );

~=""];

Case ( 

	~count = 0 ; If ( ~preKey = "." or IsEmpty ( ~preKey ) ; ~jsonInitString ; "" ) ;
	~count > 0 ;
		JSONSetElement (  
			// RECURSION
			ValueList.toJSON ( ~listUpdated ; ~format ; ~preKey ; ~postKey ) ;

			// array cell
			~preKey & If ( Right ( ~preKey ; 1 ) ≠ "." ; "." ) & 
			If ( ~format = "array" ; "[" & ~count - 1 & "]" ; ~count ) &
			If ( Left ( ~postKey ; 1 ) ≠ "." ; "." ) & ~postKey ;

			// value to store
			~value ;

			// Determine the data type
			Case ( 
				Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "{" ; JSONObject ;
				Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "[" ; JSONArray ;
				GetAsText ( GetAsTimestamp ( ~value ) ) = ~value ; JSONString;
				GetAsNumber ( ~value ) = ~value ; JSONNumber;
				GetAsText ( ~value ) = ~value ; JSONString;
				)
	 	)

	)

)