/**
 *
 * Return the primary valid data type, (e.g. 0 or 1 would be valid for both "number" and "boolean", but this function will return only "number")
 *
 * @param {string} data = data to evaluate for type
 * 
 * @returns {string} primary valid data type
 *
 * @version 1.2.0
 * @author Allen Ulbricht, https://github.com/aulbricht
 *
 * @modified 2023-10-19 ( fmdev@danshockley.com ): Empty string should be considered "text" instead of "timestamp". 
 * @modified 2023-10-19 ( fmdev@danshockley.com ): Bug-fix to handle numbers properly - previous version had a bug that didn't consider any number a number. Now use external custom function isNumeric to handle the multiple "types" of valid numbers in FileMaker. 
 *
 */


Let ([

	// Abstract the parameter
	~value = value ;

	~baseDataType = Case ( 

				// EMPTY, so assume text
				IsEmpty ( ~value ) ; "text" ;

				// JSON object types
				Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "{" ; "object" ;
				Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "[" ; "array" ;

				// timestamp
				GetAsText ( GetAsTimestamp ( ~value ) ) = ~value ; "timestamp" ;

				// date
				GetAsText ( GetAsDate ( ~value ) ) = ~value ; "date" ;

				// time
				GetAsText ( GetAsTime ( ~value ) ) = ~value ; "time" ;

				// number
				isNumeric ( ~value ) ; "number" ; /* too many things to check to do here, so call another function */

				// text
				GetAsText ( ~value ) = ~value ; "text" ;

				);

~=""];

~basedataType

)
