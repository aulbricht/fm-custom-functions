/**
 *
 * Given a text string, "sanitize" all found mustache notation in the string, standardizing to uppercase and allow characters
 *
 * @param {text} textString 
 *
 * @returnSuccess {text} sanitized text
 * 
 * @module Mustache
 * 
 * @version 1.0.0
 * @author Allen Ulbricht, https://github.com/aulbricht
 *
 */


While ( 
	//// initial variables ////
	[ 
		~textString = textString ;
		~pattern = "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_-~.[]{}" ;
		~result = ~textString ;
		~startPos = Position ( ~result ; "{{" ; 1 ; 1 ) ;
		~endPos = Position ( ~result ; "}}" ; 1 ; 1 ) ;
		~len = Length ( ~textString ) ;
		~i = 1 ;
	~=""]; 

	//// "while this is true" loop condition /////
	~startPos > 0 and ~endPos > ~startPos ; 

	//// logic ////////////////
	[ 
		~segment = Middle ( ~result ; ~startPos ; ~endPos - ~startPos + 2 ) ;
		~filteredSegment = Filter ( ~segment ; ~pattern ) ;
		~filteredSegment = Upper ( ~filteredSegment ) ;
		~result = Left ( ~result ; ~startPos - 1 ) & ~filteredSegment & Middle ( ~result ; ~endPos + 2 ; Length ( ~result ) - ~endPos - 1 ) ;
		~startPos = Position ( ~result ; "{{" ; ~startPos + 1 ; 1 ) ;
		~endPos = Position ( ~result ; "}}" ; ~startPos ; 1 ) ;
		~i = ~i + 1 ;
	~=""]; 

	//// result ///////////////
	~result
)
