/**
 *
 * Determine the JSON data type (e.g. numeric, string, etc) and return the corresponding numeral for the JSONSetElements data type argument
 *
 * @param {any} value - input data to analyze and detect the type
 *
 * @returnSuccess {text} JSON data type: text, number, time, timestamp, object, array
 * 
 * @module JSON
 * 
 * @version 1.1.1
 * @author Allen Ulbricht, https://github.com/aulbricht
 *
 */
 

Let ([

	// Abstract the input variable
	~value = value ;

	~dataType = Case ( 

		Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "{" ; "object" ;
		Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "[" ; "array" ;
		GetAsText ( GetAsTimestamp ( ~value ) ) = ~value ; "timestamp" ;
		GetAsText ( GetAsDate ( ~value ) ) = ~value ; "date" ;
		GetAsText ( GetAsTime ( ~value ) ) = ~value ; "time" ;
		GetAsTime ( ~value ) = ~value ; "number" ; // note: using GetAsTime on a number will equal the number, but not if the same value is set as text
		GetAsText ( ~value ) = ~value ; "text" ;

		)

];

// return the corresponding numeral for the JSONSetElements data type argument
Case (

	~dataType = "number" ; 2 ; // JSONNumber
	~dataType = "object" ; 3 ; // JSONObject
	~dataType = "array" ; 4 ; // JSONArray
	1 // default to JSONString

)

)