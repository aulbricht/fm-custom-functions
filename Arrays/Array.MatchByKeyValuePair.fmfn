/**
 *
 * Return the value of a specified key for the cell where the first instance of a different, given key and value pair are found to match.
 *
 * @param {array} array
 * @param {text} matchKey
 * @param {text} matchValue
 * @param {text} returnKey - associated value to return; note: if value is null, return the array cell ordinal instead (0-n)
 *
 * @returnSuccess {text} value paired to the specified returnKey for the found array cell
 *
 * @module ARRAY
 * 
 * @version 1.1.1
 * @author Allen Ulbricht, https://github.com/aulbricht
 *
 */


While ( 

	//// initial variables ////
	[ 

		~array = array ;
		~searchKey = matchKey ;
		~searchValue = matchValue ;
		~returnKey = returnKey ;

		~numCells = ValueCount ( JSONListKeys ( ~array ; "." ) );
		~i = 1 ;
		~thisValue = "" ;
		
		~=""] ; 

	//// "while this is true" loop condition ///////
	~thisValue ≠ ~searchValue and ~i ≤ ~numCells ; 

	//// logic ////////////////
	[ 

		~keybase = "[" & ~i - 1 & "]." ;
		~thisValue = JSONGetElement ( ~array ; ~keybase & ~searchKey );
		~returnValue = If ( ~thisValue = ~searchValue ; 
			Case ( 
				isempty ( ~returnKey ) ; ~i - 1 ;
				JSONGetElement ( ~array ; ~keybase & ~returnKey ) )
				);

		~i = ~i + 1 ;

		~=""] ; 

	//// result ///////////////
	~returnValue

	)